macro(recursive_collect_include_directories TARGET VAR)
    recursive_collect_property(${TARGET} ${VAR} INTERFACE_INCLUDE_DIRECTORIES)
endmacro()
macro(recursive_collect_link_libraries TARGET VAR)
    recursive_collect_property(${TARGET} ${VAR} INTERFACE_LINK_LIBRARIES)
endmacro()
function(recursive_collect_property TARGET VAR PROPERTY)
    if (NOT TARGET ${TARGET})
        message(FATAL_ERROR "'${TARGET}' is not a valid target.")
        return()
    endif ()
    get_target_property(TGT_DIRS ${TARGET} ${PROPERTY})
    get_target_property(TGT_LIBS ${TARGET} INTERFACE_LINK_LIBRARIES)
    if (NOT TGT_LIBS STREQUAL "TGT_LIBS-NOTFOUND")
        foreach (LIB ${TGT_LIBS})
            if (NOT TARGET ${LIB})
                message(FATAL_ERROR "'${LIB}' is not a valid library. (Used by ${TARGET})")
            endif ()
            recursive_collect_property(${LIB} LIB_DIRS ${PROPERTY})
            if (NOT LIB_DIRS STREQUAL "")
                list(APPEND TGT_DIRS ${LIB_DIRS})
            endif ()
        endforeach ()
    endif ()
    if (NOT TGT_DIRS STREQUAL "TGT_DIRS-NOTFOUND")
        set(${VAR} ${TGT_DIRS} PARENT_SCOPE)
    endif ()
endfunction()